#version 450
#extension GL_EXT_nonuniform_qualifier : enable

layout(local_size_x = 64, local_size_y = 1, local_size_z = 1) in;

struct Cluster {
    vec4 min;
    vec4 max;
};

layout(set = 0, binding = 8) buffer ClusterBuffer {
    Cluster clusters[];
} allClusterBuffers[];

layout(push_constant) uniform PushConstants {
    uint clusterBufferIndex;
    uint gridX;
    uint gridY;
    uint gridZ;
    mat4 invProj;
    float near;
    float far;
} pc;

vec4 screenToView(vec4 screen) {
    vec4 view = pc.invProj * screen;
    return view / view.w;
}

vec3 lineIntersectionToZPlane(vec3 A, vec3 B, float zDistance) {
    vec3 normal = vec3(0.0, 0.0, 1.0);
    vec3 ab = B - A;
    float t = (zDistance - dot(normal, A)) / dot(normal, ab);
    return A + t * ab;
}

void main() {
    uint gID = gl_GlobalInvocationID.x;
    if (gID >= pc.gridX * pc.gridY * pc.gridZ) return;

    uint z = gID / (pc.gridX * pc.gridY);
    uint y = (gID % (pc.gridX * pc.gridY)) / pc.gridX;
    uint x = gID % pc.gridX;

    vec2 texelSize = 1.0 / vec2(pc.gridX, pc.gridY);
    
    // Cluster screen space bounds
    vec4 minSS = vec4(vec2(x, y) * texelSize * 2.0 - 1.0, 0.0, 1.0);
    vec4 maxSS = vec4(vec2(x + 1, y + 1) * texelSize * 2.0 - 1.0, 0.0, 1.0);

    // Near and far Z for this cluster
    float clusterNear = pc.near * pow(pc.far / pc.near, float(z) / float(pc.gridZ));
    float clusterFar  = pc.near * pow(pc.far / pc.near, float(z + 1) / float(pc.gridZ));

    // View space corners
    vec3 minViewNear = screenToView(vec4(minSS.xy, 0.0, 1.0)).xyz;
    vec3 minViewFar  = screenToView(vec4(minSS.xy, 1.0, 1.0)).xyz;
    vec3 maxViewNear = screenToView(vec4(maxSS.xy, 0.0, 1.0)).xyz;
    vec3 maxViewFar  = screenToView(vec4(maxSS.xy, 1.0, 1.0)).xyz;

    // Intersection with cluster near and far planes
    vec3 pNearMin = lineIntersectionToZPlane(vec3(0), minViewFar, -clusterNear);
    vec3 pNearMax = lineIntersectionToZPlane(vec3(0), maxViewFar, -clusterNear);
    vec3 pFarMin  = lineIntersectionToZPlane(vec3(0), minViewFar, -clusterFar);
    vec3 pFarMax  = lineIntersectionToZPlane(vec3(0), maxViewFar, -clusterFar);

    vec3 minAABB = min(min(pNearMin, pNearMax), min(pFarMin, pFarMax));
    vec3 maxAABB = max(max(pNearMin, pNearMax), max(pFarMin, pFarMax));

    allClusterBuffers[nonuniformEXT(pc.clusterBufferIndex)].clusters[gID].min = vec4(minAABB, 0.0);
    allClusterBuffers[nonuniformEXT(pc.clusterBufferIndex)].clusters[gID].max = vec4(maxAABB, 0.0);
}
